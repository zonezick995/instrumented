import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.example.android.testing.testconfigurationsample"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        // Multiple dependency bring these files in. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }

    testOptions {
        devices {
            pixel2api29(ManagedVirtualDevice) {
                // Use device profiles you typically see in Android Studio
                device = "Pixel 2"
                apiLevel = 29
                // You can also specify "google" if you require Google Play Services.
                systemImageSource = "aosp"
                abi = "x86"
            }
            pixel2api23(ManagedVirtualDevice) {
                device = "Pixel 2"
                apiLevel = 23
                systemImageSource = "aosp"
                abi = "x86"
            }
            nexus9api29(ManagedVirtualDevice) {
                device = "Nexus 9"
                apiLevel = 29
                systemImageSource = "aosp"
                abi = "x86"
            }
        }
    }
}

dependencies {
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.activity:activity-compose:$activity_compose_version"
    androidTestImplementation "androidx.test.ext:junit:$junit_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}